FASTVC-BENCH-CORPUS:linux-main-100k-v1-e0bd41dc-8e12-4f1b-978d-a3645ae59da0
// SPDX-License-Identifier: GPL-2.0-only
///
/// Use *_pool_zalloc rather than *_pool_alloc followed by memset with 0
///
// Copyright: (C) 2015 Intel Corp.
// Options: --no-includes --include-headers
//
// Keywords: dma_pool_zalloc, pci_pool_zalloc
//

virtual context
virtual patch
virtual org
virtual report

//----------------------------------------------------------
//  For context mode
//----------------------------------------------------------

@depends on context@
expression x;
statement S;
@@

* x = \(dma_pool_alloc\|pci_pool_alloc\)(...);
  if ((x==NULL) || ...) S
* memset(x,0, ...);

//----------------------------------------------------------
//  For patch mode
//----------------------------------------------------------

@depends on patch@
expression x;
expression a,b,c;
statement S;
@@

- x = dma_pool_alloc(a,b,c);
+ x = dma_pool_zalloc(a,b,c);
  if ((x==NULL) || ...) S
- memset(x,0,...);

@depends on patch@
expression x;
expression a,b,c;
statement S;
@@

- x = pci_pool_alloc(a,b,c);
+ x = pci_pool_zalloc(a,b,c);
  if ((x==NULL) || ...) S
- memset(x,0,...);

//----------------------------------------------------------
//  For org and report mode
//----------------------------------------------------------

@r depends on org || report@
expression x;
expression a,b,c;
statement S;
position p;
@@

 x = @p\(dma_pool_alloc\|pci_pool_alloc\)(a,b,c);
 if ((x==NULL) || ...) S
 memset(x,0, ...);

@script:python depends on org@
p << r.p;
x << r.x;
@@

msg="%s" % (x)
msg_safe=msg.replace("[","@(").replace("]",")")
coccilib.org.print_todo(p[0], msg_safe)

@script:python depends on report@
p << r.p;
x << r.x;
@@

msg="WARNING: *_pool_zalloc should be used for %s, instead of *_pool_alloc/memset" % (x)
coccilib.report.print_report(p[0], msg)
/* bench 13854.4.0 684e03776f8c */
/* bench 13854.4.1 23c34d77b14e */
/* bench 13854.4.2 55ffb65957e1 */
/* bench 13854.4.3 89da2bbb0fa0 */
/* bench 13854.4.4 4e9ec9e233d5 */
/* bench 13854.4.5 76b368123387 */
/* bench 13854.4.6 1e97ce26b87c */
/* bench 13854.4.7 10bcd18a6f1d */
/* bench 13854.4.8 52f096d7b093 */
/* bench 13854.4.9 3bce134eff32 */
/* bench 13854.4.10 fa7ec1df2d38 */
/* bench 13854.4.11 8ae8756af47e */
